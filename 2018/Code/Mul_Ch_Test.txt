/*
 Using the ZeroCross Tail(ZCT)to generate a sync pulse to drive a 
 triac for dimming ac lights.
 
 Connections to an Arduino:
 1. Connect the C terminal of the ZeroCross Tail to digital pin 2 with a 10K ohm 
    pull up resistor to Arduino 5V.
 2. Connect the E terminal of the ZeroCross Tail to Arduino Gnd.
*/

#include <TimerOne.h>                    

// User data
int channels[] = {3,4,5,6}; // Triac out pins
int dimmers[] = {2,2,2,2}; // 2 = Full on   ,    127 = Full off

// Light control
const int numOfChannels = sizeof(channels)/sizeof(int);
volatile int counters[numOfChannels];
volatile boolean zero_cross[numOfChannels]; //has channel had a zero cross
int dir[numOfChannels]; // Dim direction
int freqStep = 60;              // Set to 60hz mains

// Test vars
char buffer[6];
int TestMode = 1; //0 = serial  1 = dim loop

void setup()
{
  // Set triac fire pins as output
  for (int i = 0; i < numOfChannels; i++) {
    pinMode(channels[i], OUTPUT);
    counters[i] = 0;
    zero_cross[i] = 0;
    dir[i] = 1;
  }
  attachInterrupt(0, zero_cross_detect, RISING);   // Attach an Interupt to digital pin 2 (interupt 0),
  Timer1.initialize(freqStep);
  Timer1.attachInterrupt(dim_check,freqStep);
  Serial.begin(9600);
}

void loop()                        // Main loop
{
  if(TestMode == 0)
  {
    // Change dimming manually over serial port
    int index;  
    while (Serial.available()) 
    {
      index = Serial.readBytesUntil('\n', buffer, 5);  //newline or max of 5 chars
      buffer[index] = '\0';
    
      int n = atoi(buffer);  //convert readString into a number
      Serial.println(n); //so you can see the integer
      for (int i = 0; i < numOfChannels; i++)
      {
        dimmers[i] = n;
      }
      buffer[0] = '\0';
    } 
    Serial.println(dimmers[0]);
  }
  else
  {
    // Dimm channels overtime
    for (int i = 0; i < numOfChannels; i++)
    {
      // Change dim up and back down
      if(dimmers[i] > 128 || dimmers[i] < 2)
      {
        dir[i] *= -1;
      }
      dimmers[i] += dir[i];
    }
    delay(50);
  }
}

// This function will fire the triac at the proper time
void dim_check()
{
  for (int i = 0; i < numOfChannels; i++)
  {
    // First check to make sure the zero-cross has happened else do nothing
    if(zero_cross[i] == 1)
    {
      if(counters[i]>=dimmers[i])
      {
        delayMicroseconds(100); //These values will fire the triac.
        digitalWrite(channels[i], HIGH);
        delayMicroseconds(50);
        digitalWrite(channels[i], LOW); 
        counters[i] = 0;  // Reset the accumulator
        zero_cross[i] = 0;  // Reset the zero_cross so it may be turned on again at the next zero_cross_detect    
      }
      else
      {
        counters[i]++;  // If the dimming value has not been reached, increment the counter
      }
    }
  }
}

// Set the boolean to true to tell our dimming function that a zero cross has occured
void zero_cross_detect()
{
  for (int i = 0; i < numOfChannels; i++)
  {
    zero_cross[i] = 1;
  }
} 